Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_KAP_SeparationSet(DEC_Laboratory_Flask_Glass_Installation_B_Empty_A_25344450-3a06-4450-9df3-132336578019);
DB_KAP_SeparationSet(DEC_Laboratory_Flask_Glass_Installation_C_Empty_A_f4117a4e-3ece-4816-8f6f-1cf3fc11a2ee);
KBSECTION
// FIRST CHECK CATALYST AND POTIONS FOR SEPARATE
IF
RequestCanCombine(_Player,_Lab,_Slot1,_Slot2,_,_,_RequestID)
AND
GetTemplate(_Lab, _LabTemplate)
AND
DB_KAP_SeparationSet(_LabTemplate)
AND
GetStatString(_Slot1, _Slot1Stat)
AND
_Slot1Stat == "OBJ_Laboratory_Glass"
AND
GetStatString(_Slot2, _Slot2Stat)
AND
_Slot2Stat == "OBJ_Laboratory_Glass"
AND
QRY_KAP_Clear_Old_SeparateVariables()
AND
QRY_KAP_CatalystIsInItemInventory(_Slot1)
THEN
//SetDualEntityEvent(_Player,_Player,"START RequestCanCombine");
DB_KAP_Current_Separate(_Player,_Lab,_Slot1);
IterateInventory(_Slot1,"KAP_Collect_Ingredients_For_Separate","KAP_Collect_Ingredients_For_Separate_Finish");
//
// ITERATE SLOT 1
//REGION ITERATE_POTIONS
//
IF
EntityEvent((ITEM)_Item,"KAP_Collect_Ingredients_For_Separate")
AND
GetTemplate(_Item,(ITEMROOT)_ItemTemplate)
AND
DB_KAP_Recipes(_,_ItemTemplate,_,_Affinity) 
AND
IsSubstring(_Affinity,"ALCH_Affinity",1)
AND
GetStackAmount(_Item, _Amount,_)
AND
QRY_KAP_Potions_Add_For_Separate(_ItemTemplate,_Amount)
THEN
DB_NOOP(1);
// 
// ADD INGREDIENTS TO DB_KAP_PotionsForSeparate
// Existed ingredients
QRY
QRY_KAP_Potions_Add_For_Separate((ITEMROOT)_ItemTemplate,(INTEGER)_Amount)
AND
DB_KAP_PotionsForSeparate(_Index,_ItemTemplate,_CollectedAmount)
AND
IntegerSum(_CollectedAmount, _Amount, _NewAmount)
THEN
NOT DB_KAP_PotionsForSeparate(_Index,_ItemTemplate,_CollectedAmount);
DB_KAP_PotionsForSeparate(_Index,_ItemTemplate,_NewAmount);
// 
// New ingredients
QRY
QRY_KAP_Potions_Add_For_Separate((ITEMROOT)_ItemTemplate,(INTEGER)_Amount)
AND
NOT DB_KAP_PotionsForSeparate(_,_ItemTemplate,_)
AND
Random(100000,_RandomIndex)
THEN
DB_KAP_PotionsForSeparate(_RandomIndex,_ItemTemplate,_Amount);

//END_REGION
//
// START COMBINE
//
// ERROR FROM MAIN SCRIPT
IF
Combined(_Lab,_,_,_,_,_Player,_LabCombinedResult)
AND
GetTemplate(_Lab, _LabTemplate)
AND
DB_KAP_SeparationSet(_LabTemplate)
AND
DB_Singleton("DB_KAP_StartCombined_Error",1)
THEN
RequestDelete((ITEM)_LabCombinedResult);
//
// ERRORS IF NOT CATALYST OR POTIONS FOR SEPARATE
IF
Combined(_Lab,_,_,_,_,_Player,_LabCombinedResult)
AND
GetTemplate(_Lab, _LabTemplate)
AND
DB_KAP_SeparationSet(_LabTemplate)
AND
NOT DB_Singleton("DB_KAP_StartCombined_Error",1)
AND
NOT DB_KAP_Current_Separate(_,_,_)
THEN
ApplyStatus(_Player, "KAP_MESSAGE_ERROR_NOT_SEPARATE", 6.0, 1);
RequestDelete((ITEM)_LabCombinedResult);
//
IF
Combined(_Lab,_,_,_,_,_Player,_LabCombinedResult)
AND
GetTemplate(_Lab, _LabTemplate)
AND
DB_KAP_SeparationSet(_LabTemplate)
AND
NOT DB_Singleton("DB_KAP_StartCombined_Error",1)
AND
NOT DB_KAP_PotionsForSeparate(_,_,_)
THEN
ApplyStatus(_Player, "KAP_MESSAGE_ERROR_NOT_SEPARATE", 6.0, 1);
RequestDelete((ITEM)_LabCombinedResult);
//
// GOOD START
IF
Combined(_Lab,_Slot1,_Slot2,_,_,_Player,_LabCombinedResult)
AND
GetTemplate(_Lab, _LabTemplate)
AND
DB_KAP_SeparationSet(_LabTemplate)
AND
NOT DB_Singleton("DB_KAP_StartCombined_Error",1)
AND
DB_KAP_Current_Separate(_Player,_Lab,_Slot1)
AND
SysCount("DB_KAP_PotionsForSeparate",3,_Amount)
AND
_Amount > 0
THEN
DB_KAP_Current_Lab(_Lab,_Player,_Slot2); // need for PROC_KAP_Create_Result
ObjectTimerLaunch(_LabCombinedResult, "KAP_DeleteLabCombinedResult_Timer", 5);
PROC_KAP_StartSeparate();
PROC_KAP_Try_Create_Result();
PROC_KAP_Try_Remove_Spented();
//
// Separate by stack
PROC
PROC_KAP_StartSeparate()
AND
DB_KAP_PotionsForSeparate(_Index,_Template,_PotionAmount)
THEN
PROC_KAP_ProcessSeparateStack(_Index,_PotionAmount);
//
// CYCLE
// Separate by one potion in stack
PROC
PROC_KAP_ProcessSeparateStack((INTEGER)_Index,(INTEGER)_Stack)
AND
IntegerSubtract(_Stack, 1, _NewStack)
AND
_NewStack >= 0
THEN
PROC_KAP_CollectAffinitiesByTemplate(_Index); // FIND_INGREDIENTS for potions
PROC_KAP_SeparateTryRegisterResult(_Index);
PROC_KAP_ProcessSeparateStack(_Index,_NewStack); // CYCLE by stack
//
//
//REGION FIND_INGREDIENTS
//
// Collect by index
PROC
PROC_KAP_CollectAffinitiesByTemplate((INTEGER)_Index)
AND
DB_KAP_PotionsForSeparate(_Index,_Template,_)
AND
DB_KAP_Recipes(_,_Template,_MainTemplate,_Affinity) 
AND
IsSubstring(_Affinity,"ALCH_Affinity",1) // only affinities recipes
THEN
DB_Singleton("DB_KAP_CurrentAffinitiesCount",0);
SysClear("DB_KAP_CurrentAffinitiesByTemplate",2);
DB_KAP_CurrentMainTemplate(_MainTemplate); // Main ingredient
PROC_KAP_CollectAffinities(_Affinity);
// 
// COUNT affinities by air, earth etc
PROC
PROC_KAP_CollectAffinities((STRING)_Affinity)
AND
DB_KAP_Affinities(_Affinity,_SecondTemplate,_,_,_,_)
AND
QRY_KAP_CollectAffinities(_SecondTemplate)
THEN
DB_NOOP(1);
// 
// A count is needed to then randomly select from all the affinities
QRY
QRY_KAP_CollectAffinities((ITEMROOT)_SecondTemplate)
AND
DB_Singleton("DB_KAP_CurrentAffinitiesCount",_Count)
AND
IntegerSum(_Count,1,_NewCount)
THEN
DB_Singleton("DB_KAP_CurrentAffinitiesCount",_NewCount);
DB_KAP_CurrentAffinitiesByTemplate(_NewCount,_SecondTemplate);

//END_REGION

//
//
//REGION REGISTER_RESULTS
//
//
PROC
PROC_KAP_SeparateTryRegisterResult((INTEGER)_Index)
AND
DB_KAP_PotionsForSeparate(_Index,_,_)
AND
DB_KAP_Current_Separate(_,_,_Slot1)
AND
SysCount("DB_KAP_CurrentMainTemplate",1,_MainAmount)
AND
_MainAmount > 0
AND
SysCount("DB_KAP_CurrentAffinitiesByTemplate",2,_SecondAmount)
AND
_SecondAmount > 0
THEN
PROC_KAP_Separate_Potion_Remove(_Slot1,_Index);
PROC_KAP_Separate_Ingredients_Add();
PROC_KAP_Separate_Catalyst_Remove(_Slot1);
//
// Spent processed potion
PROC
PROC_KAP_Separate_Potion_Remove((ITEM)_Slot,(INTEGER)_Index)
AND
DB_KAP_PotionsForSeparate(_Index,_Template,_AvailableAmount)
AND
IntegerSubtract(_AvailableAmount,1,_NewAvailableAmount)
AND
QRY_KAP_Add_Ingredients_Spent(_Slot,_Template,1)
THEN
NOT DB_KAP_PotionsForSeparate(_Index,_Template,_AvailableAmount);
DB_KAP_PotionsForSeparate(_Index,_Template,_NewAvailableAmount);
//
// Add separated ingredients to results
PROC
PROC_KAP_Separate_Ingredients_Add()
AND
DB_KAP_Current_Separate(_,_Lab,_)
AND 
DB_KAP_CurrentMainTemplate(_MainTemplate)
AND
SysCount("DB_KAP_CurrentAffinitiesByTemplate",2,_Amount)
AND
Random(_Amount,_RandomNumber)
AND
IntegerSum(_RandomNumber, 1, _RandomNumberPlusOne) // because Random() is within [0, Modulo-1]
AND
DB_KAP_CurrentAffinitiesByTemplate(_RandomNumberPlusOne,_SecondTemplate)
THEN
//SetDualEntityEvent(_Lab,_Lab,"PROC_KAP_Separate_Ingredients_Add");
PROC_KAP_Register_Result(_MainTemplate,_Lab,1); // Register the result for later creation
PROC_KAP_Register_Result(_SecondTemplate,_Lab,1); // Register the result for later creation
//TemplateAddTo(_MainTemplate,_Slot,1,0);
//TemplateAddTo(_SecondTemplate,_Slot,1,0);
//
// Catalyst remove (checked on RequestCanCombine)
PROC
PROC_KAP_Separate_Catalyst_Remove((ITEM)_Slot1)
AND
DB_KAP_CatalystTemplate(_Template)
AND
NOT DB_OnlyOnce("PROC_KAP_Separate_Catalyst_Remove")
THEN
TemplateRemoveFrom((ITEMROOT)_Template, _Slot1, 1);
DB_OnlyOnce("PROC_KAP_Separate_Catalyst_Remove");

//END_REGION


//REGION TECHNICAL
// Singleton for DB_KAP_CurrentMainTemplate 
//
IF
DB_KAP_CurrentMainTemplate((ITEMROOT)_NewMainTemplate)
AND
DB_KAP_CurrentMainTemplate(_OldMainTemplate)
AND
_NewMainTemplate != _OldMainTemplate
THEN
NOT DB_KAP_CurrentMainTemplate(_OldMainTemplate);
//
//
// QRY for clear db on start combine
QRY
QRY_KAP_Clear_Old_SeparateVariables()
THEN
SysClear("DB_KAP_Current_Separate",3);
SysClear("DB_KAP_PotionsForSeparate",3);
SysClear("DB_KAP_CurrentAffinitiesByTemplate",2);
NOT DB_OnlyOnce("QRY_KAP_CatalystIsInItemInventory_Checked");
NOT DB_OnlyOnce("PROC_KAP_Separate_Catalyst_Remove");

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "GLO_KelaAlchemyProject"
